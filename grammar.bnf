<document> ::= [ <template-directive> ] { <element> }
<template-directive> ::= "template!" <include-name>

<element>  ::= <element-named>
             | <element-text>
             | <element-implicit-div>
             | <style-tag>
             | <eval-tag>
             | <include-directive>
             | <content-directive>
             | <raw-html>

<element-named> ::= NAME <element-named-rest>
<element-implicit-div> ::= ( <element-data> ) <element-named-rest>
<element-named-rest> ::= { <element-data> } [ <content> ]
<style-tag> ::= "style" <css-data>
<eval-tag> ::= ( "eval" | "eval!" ) "{" TEXT "}"
<include-directive> ::= "include" <include-name>
<raw-html> ::= "__html__" "{" TOKENS "}"
<include-name> ::= ( "(" TOKENS ")" | NAME )
<content-directive> ::= "content!"

<element-data> ::= <attr-list> | <inline-style> | CLASS | ID
<attr-list> ::= "[" { <attr> [ "," ] } "]"
<inline-style> ::= "%" <css-data>
<css-data> ::= DELIMITER CSS DELIMITER
<attr> ::= NAME "=" VALUE
<element-text>  ::= [ TEXT ] <text-body>
<text-body> ::= "(" TOKENS ")"
<content>  ::= "{" { <element> } "}" | <text-body> | ">" <element>